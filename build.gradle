/*
 * Copyright (c) 2014, Francis Galiegue (fgaliegue@gmail.com)
 *
 * This software is dual-licensed under:
 *
 * - the Lesser General Public License (LGPL) version 3.0 or, at your option, any
 *   later version;
 * - the Apache Software License (ASL) version 2.0.
 *
 * The text of this file and of both licenses is available at the root of this
 * project or, if you have the jar distribution, in directory META-INF/, under
 * the names LGPL-3.0.txt and ASL-2.0.txt respectively.
 *
 * Direct link to the sources:
 *
 * - LGPL 3.0: https://www.gnu.org/licenses/lgpl-3.0.txt
 * - ASL 2.0: http://www.apache.org/licenses/LICENSE-2.0.txt
 */

apply(plugin: "java");
apply(plugin: "maven");
apply(plugin: "signing");
apply(plugin: "osgi");
apply(plugin: "idea");
apply(plugin: "eclipse");

group = "com.github.java-json-tools";
version = "2.2.8";
sourceCompatibility = "1.6";
targetCompatibility = "1.6"; // defaults to sourceCompatibility

/*
 * Repositories to use
 */
repositories {
    mavenCentral();
}

/*
 * List of dependencies
 */
dependencies {
    compile(group: "com.github.java-json-tools", name: "json-schema-core", version: "1.2.8");
    compile(group: "javax.mail", name: "mailapi", version: "1.4.3");
    compile(group: "joda-time", name: "joda-time", version: "2.9.7");
    compile(group: "com.googlecode.libphonenumber", name: "libphonenumber", version: "8.0.0");
    compile(group: "com.google.code.findbugs", name: "jsr305", version: "3.0.1");
    compile(group: "net.sf.jopt-simple", name: "jopt-simple", version: "5.0.3");
    testCompile(group: "org.testng", name: "testng", version: "6.10") {
        exclude(group: "junit", module: "junit");
        exclude(group: "org.beanshell", module: "bsh");
        exclude(group: "org.yaml", module: "snakeyaml");
    };
    testCompile(group: "org.mockito", name: "mockito-core", version: "2.4.2");
    testCompile(group: "org.easytesting", name: "fest-assert", version: "1.4");
}

javadoc.options.links("http://docs.oracle.com/javase/6/docs/api/");
javadoc.options.links("http://jsr-305.googlecode.com/svn/trunk/javadoc/");
javadoc.options.links("http://fasterxml.github.com/jackson-databind/javadoc/2.2.0/");
javadoc.options.links("http://fasterxml.github.com/jackson-core/javadoc/2.2.0/");
javadoc.options.links("http://docs.guava-libraries.googlecode.com/git-history/v16.0.1/javadoc/");
javadoc.options.links("http://fge.github.io/btf/");
javadoc.options.links("http://fge.github.io/msg-simple/");
javadoc.options.links("http://fge.github.io/jackson-coreutils/");
javadoc.options.links("http://fge.github.io/uri-template/");
javadoc.options.links("http://java-json-tools.github.io/json-schema-core/1.2.x/");


/*
 * Necessary! Otherwise TestNG will not be used...
 *
 * Also, we don't want gradle's default HTML report: it does not support
 * parameterized tests which I use a _lot_.
 */
test {
    useTestNG() {
        useDefaultListeners = true;
    };
}

/*
 * Necessary to generate the source and javadoc jars
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources";
    from sourceSets.main.allSource;
}

/*
 * Javadoc: we need to tell where the overview.html is, it will not pick it up
 * automatically...
 */

javadoc.options {
    overview = "src/main/javadoc/overview.html";
    extDirs = [ file("src/main/javadoc") ];
    docFilesSubDirs = true;
}

/*
 * Equally annoyingly, the docFilesSubDirs option above does not seem to have
 * any effect :/
 */
task copyDocFiles(dependsOn: javadoc) {
    doLast {
        copy {
            from("src/main/javadoc") {
                include "**/doc-files/**";
            };
            into javadoc.destinationDir;
        }
    }
}

task javadocJar(type: Jar, dependsOn: copyDocFiles) {
    classifier = "javadoc";
    from javadoc.destinationDir;
}

/*
 * Creates a jar that can be used as a library on java projects.
 * This jar already includes all the dependencies.
 */
task libJar(type: Jar, dependsOn: jar) {
    classifier = "lib";
    from {
        configurations.compile.collect { zipTree(it) }
    };
    with jar;
}

jar {
    manifest {
        attributes("Main-Class": "com.github.fge.jsonschema.main.cli.Main");
    }
}
artifacts {
    archives jar;
    archives sourcesJar;
    archives javadocJar;
    archives libJar;
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5";
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip";
}

task pom {
    doLast {
        pom {}.writeTo("${projectDir}/pom.xml");
    }
}

/*
 * SIGNING
 */

project.ext {
    description = "A Java implementation of the JSON Schema specification";
    scmUrl = sprintf("git@github.com:box-metadata/%s", name);
    projectURL = sprintf("https://github.com/box-metadata/%s", name);
    sonatypeStaging = "https://oss.sonatype.org/service/local/staging/deploy/maven2/";
    sonatypeSnapshots = "https://oss.sonatype.org/content/repositories/snapshots/";
};

task checkSigningRequirements {
    doLast {
        def requiredProperties = [ "sonatypeUsername", "sonatypePassword" ];
        def noDice = false;
        requiredProperties.each {
            if (project.properties[it] == null) {
                noDice = true;
                System.err.printf("property \"%s\" is not defined!", it);
            }
        }
        if (noDice)
            throw new IllegalStateException("missing required properties for " +
                "upload");
    }
}

uploadArchives {
    dependsOn(checkSigningRequirements);
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: "${sonatypeStaging}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }

            snapshotRepository(url: "${sonatypeSnapshots}") {
                authentication(
                    userName: project.properties["sonatypeUsername"],
                    password: project.properties["sonatypePassword"]
                );
            }
        }
    }
}

/*
 * Configure pom.xml on install, uploadArchives
 */
[
    install.repositories.mavenInstaller,
    uploadArchives.repositories.mavenDeployer
]*.pom*.whenConfigured { pom ->
    pom.project {
        name "${project.name}";
        packaging "jar";
        description "${project.ext.description}";
        url "${projectURL}";

        scm {
            url "${scmUrl}";
            connection "scm:git:${scmUrl}";
            developerConnection "scm:git:${scmUrl}";
        }

        licenses {
            license {
                name "Lesser General Public License, version 3 or greater";
                url "http://www.gnu.org/licenses/lgpl.html";
                distribution "repo";
            };
            license {
                name "Apache Software License, version 2.0";
                url "http://www.apache.org/licenses/LICENSE-2.0";
                distribution "repo";
            }
        }

        developers {
            developer {
                id "huggsboson";
                name "John Huffaker";
                email "jhuffaker+java-json-tools@gmail.com";
            }
        }
    }
}

ext.forRelease = !version.endsWith("-SNAPSHOT");
signing {
    required { forRelease && gradle.taskGraph.hasTask("uploadArchives") };
    sign configurations.archives;
}

